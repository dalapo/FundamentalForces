buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'org.parchmentmc:librarian:1.+'
    }

}
plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'


version = "${project.minecraftVersion}-${project.modVersion}"
group = "com.${project.groupId}.${project.modId}"
archivesBaseName = project.modId
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets {
    main.resources.srcDirs += 'src/main/generated_resources'
}

mixin {
    add sourceSets.main, "${project.modId}.refmap.json"
}

minecraft {
    mappings channel: 'parchment', version: '2022.03.13-1.18.2'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=" + archivesBaseName + ".mixins.json"

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                self {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            arg "-mixin.config=" + archivesBaseName + ".mixins.json"

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                self {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', "${project.modId}", '--all', '--output', file('src/main/generated_resources/'), '--existing', file('src/main/resources/')

            mods {
                self {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name 'Curios maven'
        url "https://maven.theillusivec4.top/"
    }
    maven {
        name 'JEI maven'
        url "https://dvs1.progwml6.com/files/maven"
    }
    maven { url = 'https://maven.blamejared.com/' }
    maven {
        name "CTM Maven"
        url "https://maven.tterrag.com/"
    }
    maven {
        name "Moonflower Maven"
        url "https://repo.moonflower.gg/releases"
    }
    maven {
        name = "Curse Maven"
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { // Registrate
        url "https://maven.tterrag.com/"
    }
    mavenLocal()
}


configurations {
    shade
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'com.tterrag.registrate', 'team.lodestar.fufo.repack.registrate'
}

reobf {
    shadowJar {}
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraftVersion}-${project.forgeVersion}"

    def registrate = "com.tterrag.registrate:Registrate:${registrateVersion}"
    implementation fg.deobf(registrate)
    shade registrate

    if (System.getProperty("idea.sync.active") != "true") {
        annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    }

    implementation fg.deobf('gg.moonflower:pollen-forge:1.4.0+1.18.2') {
        transitive = false
    }

    implementation fg.deobf("team.lodestar.lodestone:lodestone:${project.lodestoneVersion}")

    implementation fg.deobf("curse.maven:curios-309927:3748873")


    compileOnly fg.deobf('mezz.jei:jei-1.18.2:9.7.0.193:api')
    runtimeOnly fg.deobf('mezz.jei:jei-1.18.2:9.7.0.193')

//    runtimeOnly fg.deobf("team.chisel.ctm:CTM:MC1.16.1-1.1.1.5")

}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Fundamental Forces",
                "Specification-Vendor"    : "Lodestar",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Lodestar",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "${project.modId}.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}


